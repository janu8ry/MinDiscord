import groovy.json.JsonSlurper

apply plugin: 'java'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'


sourceCompatibility = 16

sourceSets.main {
    java.srcDirs = ['src']
    resources.srcDirs = ['src/resources']
}

repositories {
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
}

ext {
    def slurper = new JsonSlurper()
    def pluginFile = file("${sourceSets.main.resources.srcDirs.last()}/plugin.json")
    def plugin = slurper.parseText(pluginFile.text)

    artifactBaseName = plugin.name + (!plugin.version.isEmpty() ? "-$plugin.version" : "")

    versions = [:]
    versions.mindustry = 'v' + plugin.minGameVersion
}

dependencies {
    implementation 'org.json:json:20180130'
    compileOnly "com.github.Anuken.Arc:arc-core:$versions.mindustry"
    compileOnly "com.github.Anuken.Mindustry:core:$versions.mindustry"
}


jar {
    archiveBaseName.set artifactBaseName
    from {
        configurations.runtimeClasspath.collect{it.isDirectory() ? it : zipTree(it) }
    }
}
